plugins {
    id 'java'
    id 'jacoco'
    id 'com.auth0.gradle.oss-library.java'
}

group = 'com.auth0'

logger.lifecycle("Using version ${version} for ${name} group $group")

def signingKey = findProperty('signingKey')
def signingKeyPwd = findProperty('signingPassword')

oss {
    name 'jwks-rsa'
    repository 'jwks-rsa-java'
    organization 'auth0'
    description 'JSON Web Key Set parser library'
    baselineCompareVersion '0.15.0'
    skipAssertSigningConfiguration true

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
        hzalaz {
            displayName = 'Hernan Zalazar'
            email = 'hernan@auth0.com'
        }
    }
}

signing {
    useInMemoryPgpKeys(signingKey, signingKeyPwd)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility '1.8'
    targetCompatibility '1.8'
}

test {
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.15.0'
    implementation (group: 'com.google.guava', name: 'guava', version:'32.1.1-jre') {
        // needed due to https://github.com/google/guava/issues/6654
        exclude group: "org.mockito", module: "mockito-core"
    }
    testImplementation group: 'junit', name: 'junit', version:'4.13.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version:'1.10.19'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version:'1.3'
}

// See https://github.com/google/guava/releases/tag/v32.1.0 for why this is required
sourceSets.all {
    configurations.getByName(runtimeClasspathConfigurationName) {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
    configurations.getByName(compileClasspathConfigurationName) {
        attributes.attribute(Attribute.of("org.gradle.jvm.environment", String), "standard-jvm")
    }
}